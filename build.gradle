plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "com.vanniktech.maven.publish" version "0.30.0"
}

group = 'io.github.kaleert'
version = '1.1.0'

java {
    sourceCompatibility = '21'
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-web'
    
    implementation 'com.mysql:mysql-connector-j'
    
    implementation 'org.yaml:snakeyaml'
    
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.2'
    
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.16.1' 
    
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.16.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    
    testImplementation 'com.github.javaparser:javaparser-core:3.25.7'
    
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    
    all {
        exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-jaxb-annotations'
    }
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
    archiveClassifier = ''
}

tasks.named('shadowJar') {
    archiveClassifier = 'shadow'
    mergeServiceFiles() 
}

tasks.named('test') {
    useJUnitPlatform()
}

task generateDocs(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.kaleert.nyagram.docgen.DocGenerator'
}

mavenPublishing {
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
    
    signAllPublications()

    coordinates(group, "nyagram", version)

    pom {
        name = "Nyagram"
        description = "Modern Java Telegram Bot Framework based on Spring Boot 3"
        inceptionYear = "2025"
        url = "https://nyagram.kaleert.pro"

        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
                distribution = "repo"
            }
        }

        developers {
            developer {
                id = "kaleert"
                name = "Kaleert"
                url = "https://github.com/Kaleert"
            }
        }

        scm {
            url = "https://github.com/Kaleert/nyagram"
            connection = "scm:git:git://github.com/Kaleert/nyagram.git"
            developerConnection = "scm:git:ssh://git@github.com/Kaleert/nyagram.git"
        }
    }
}

javadoc {
    options {
        encoding = 'UTF-8'
        tags = [ 'example:a:Example:' ] 
        addStringOption('Xdoclint:none', '-quiet') 
    }
}